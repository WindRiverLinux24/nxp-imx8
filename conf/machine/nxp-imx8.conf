#@TYPE: Machine
#@NAME: nxp-imx8
#@DESCRIPTION: nxp-imx8

DEFAULTTUNE = "aarch64"
require conf/machine/include/arm/arch-armv8a.inc

MACHINE_FEATURES = "ext3 pci usbgadget usbhost vfat alsa"

DISTRO_FEATURES:append = " wifi bluetooth pci"

KERNEL_IMAGETYPE = ""
KERNEL_IMAGETYPES = "Image.gz Image"

DTB_FILES = "imx8qm-mek.dtb imx8qm-mek-dsi-rm67191.dtb imx8qm-mek-ov5640.dtb imx8qm-mek-dsi-rm67199.dtb \
		imx8qm-mek-hdmi.dtb imx8mq-evk-dcss-rm67191.dtb imx8mq-evk-lcdif-rm67199.dtb imx8mq-evk.dtb \
		imx8mq-evk-dcss-adv7535.dtb"

KERNEL_DEVICETREE = "${@''.join('freescale/%s ' % d for d in '${DTB_FILES}'.split())}"

SERIAL_CONSOLE = "115200 ttyLF0"
SERIAL_CONSOLES_CHECK ?= "${SERIAL_CONSOLES}"

MULTILIBS ?= "multilib:lib32"
DEFAULTTUNE:virtclass-multilib-lib32 ?= "armv7athf-neon"

WKS_FILE ?= "sdimage-ubootheader-imx8.wks"
do_image_wic[depends] += "mtools-native:do_populate_sysroot dosfstools-native:do_populate_sysroot"

PREFERRED_PROVIDER_virtual/kernel ??= "linux-yocto"
PREFERRED_VERSION_linux-yocto ?= "6.6%"

MACHINE_EXTRA_RRECOMMENDS = " kernel-modules kernel-devicetree linux-firmware-ath10k linux-firmware-imx-sdma-imx7d"
IMAGE_FSTYPES += "tar.bz2 wic wic.bmap"
IMAGE_INSTALL:append = " kernel-devicetree kernel-image-image"

IMAGE_BOOT_FILES ?= "Image ${DTB_FILES}"

BSP_NXP_DERIVED:append=" nxp-imx8"

MACHINEOVERRIDES:prepend = "mx8:mx8-nxp-bsp:imx-nxp-sdk:use-nxp-bsp:mx8-generic-bsp:"

OPTEEOS_CONFIG = "imx-mx8qmmek imx-mx8mqevk"

MACHINE_FEATURES:append = " optee"

NON_MULTILIB_RECIPES:append = " optee-os"
NON_MULTILIB_RECIPES:append = " optee-test"
NON_MULTILIB_RECIPES:append = " optee-client"
NON_MULTILIB_RECIPES:append = " optee-os-tadevkit"
NON_MULTILIB_RECIPES:append = " libts"

MACHINE_SOCARCH = "nxp_imx8"

# For ostree
# The loader is flashed into the device separately and not part of the SD image
# No u-boot
OSTREE_BOOTLOADER_INCLUDE ?= ""
OSTREE_SD_UBOOT_WIC1 ?= ""
OSTREE_SD_UBOOT_WIC2 ?= ""
OSTREE_SD_UBOOT_WIC3 ?= ""
OSTREE_SD_UBOOT_WIC4 ?= ""
OSTREE_SD_BOOT_ALIGN ?= "10240"
OSTREE_WKS_EFI_SIZE ?= "--size=64M"
OSTREE_FDISK_BLM ?= "20480"
# Because of the size of the offset being 20480 which is ~10M, 10 must be added
# to 64
OSTREE_FDISK_FSZ ?= "74"
OSTREE_UBOOT_CMD ?= "booti"

OSTREE_BOOTSCR_PRECMD ?= " \
setenv no_menu yes; \
if test ! -n \${devnum}; then setenv devnum 1; fi; \
setenv skip_script_fdt yes; \
if test "\${board}" = imx8mq_evk; then \
  setenv fdt_file imx8mq-evk.dtb; \
  setenv kernel_addr 0x40480000; \
  setenv fdt_addr 0x44000000; \
  setenv initrd_addr 0xc8000000; \
  setenv console "ttymxc0"; \
  setenv baudrate "115200"; \
else \
  setenv fdt_file $default_dtb; \
  setenv kernel_addr 0x80280000; \
  setenv fdt_addr 0x8d000000; \
  setenv initrd_addr 0x98000000; \
  setenv console "ttyLP0"; \
  setenv baudrate "115200"; \
fi; \
"

INITRAMFS_IMAGE ??= ""
IMAGE_BOOT_FILES:append = " ${@bb.utils.contains('DISTRO_FEATURES', 'ostree', '${INITRAMFS_IMAGE}-${MACHINE}.${INITRAMFS_FSTYPES};initramfs boot.scr', '', d)} \
"
OSTREE_KERNEL ?= "Image"
DEFAULT_DTB ?= "imx8qm-mek.dtb"
OSTREE_COPY_IMAGE_BOOT_FILES = "1"
OSTREE_UBOOT_CMD ?= "booti"
OSTREE_WKS_EFI_SIZE ?= "--size=64M"

# For ustart.img
OSTREE_NETINST_DEV ?= "/dev/mmcblk1,/dev/sda,/dev/vda"
OSTREE_NETINST_ARGS ?= "${OSTREE_CONSOLE} bl=ufsd rdinit=/install instdev=${OSTREE_NETINST_DEV} instname=wrlinux instbr=\$BRANCH insturl=\$URL instab=${OSTREE_USE_AB} instsf=1 instdate=BUILD_DATE"
